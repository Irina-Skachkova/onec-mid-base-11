#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Д_Ответственный = Пользователи.ТекущийПользователь();
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		ЗаполнитьНаОснованииРеализацииТоваровУслуг(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьНаОснованииРеализацииТоваровУслуг(ДанныеЗаполнения)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Организация КАК Д_Организация,
	|	РеализацияТоваровУслуг.Контрагент КАК Д_Контрагент,
	|	РеализацияТоваровУслуг.Договор КАК Д_Договор,
	|	РеализацияТоваровУслуг.Основание КАК Д_Основание,
	|	РеализацияТоваровУслуг.Ответственный КАК Д_Ответственный,
	|	РеализацияТоваровУслуг.Комментарий КАК Д_Комментарий,
	|	РеализацияТоваровУслуг.Д_КонтактноеЛицо КАК Д_КонтактноеЛицо,
	|	РеализацияТоваровУслуг.Товары.(
	|		Ссылка КАК Ссылка,
	|		НомерСтроки КАК НомерСтроки,
	|		Номенклатура КАК Д_Номенклатура,
	|		Количество КАК Д_Количество) КАК Д_Товары
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
	
	ТоварыОснования = Выборка.Д_Товары.Выбрать();
	Пока ТоварыОснования.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Д_Товары.Добавить(), ТоварыОснования);
	КонецЦикла;
	
	Д_Основание = ДанныеЗаполнения;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли